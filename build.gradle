apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'idea'

repositories {
        maven { url 'https://artifactory.ida.digital.cabinet-office.gov.uk/artifactory/whitelisted-repos' }
}

project.ext {
    version_number = '0.2.0'
    openSamlVersion = '3.3.0'
    samlMetaDataBindingVersion = '47'
}

dependencies {
    compile(
        'io.dropwizard:dropwizard-core:1.1.2',
        'org.json:json:20170516',
        "org.opensaml:opensaml-core:$openSamlVersion",
        "uk.gov.ida:saml-metadata-bindings:$openSamlVersion-$samlMetaDataBindingVersion",
        "org.opensaml:opensaml-saml-impl:$openSamlVersion",
        "uk.gov.ida:saml-serializers:$openSamlVersion-18",
        "uk.gov.ida:saml-security:$openSamlVersion-40"
    )
    testCompile(
        'junit:junit:4.12',
        'io.dropwizard:dropwizard-testing:1.1.0',
        'org.mockito:mockito-core:2.7.22',
        "uk.gov.ida:saml-metadata-bindings-test:$openSamlVersion-$samlMetaDataBindingVersion",
        "uk.gov.ida:common-test-utils:2.0.0-31",
        "org.jsoup:jsoup:1.10.3",
    )
    testCompile('com.github.tomakehurst:wiremock:2.5.1'){ transitive = false }
}

sourceSets {
    test {
        resources {
            srcDir 'src/../configuration'
        }
    }

    acceptanceTest {
        java {
            srcDir 'src/acceptance-test/java'
        }
        resources {
            srcDir 'src/acceptance-test/resources'
            srcDir 'src/../configuration'
        }
        compileClasspath += sourceSets.main.runtimeClasspath
        compileClasspath += sourceSets.test.runtimeClasspath
        compileClasspath += sourceSets.test.output
    }
}

def build_number = System.getenv('BUILD_NUMBER') != null ? System.getenv('BUILD_NUMBER') : 'SNAPSHOT'
version = "$version_number-" + build_number

distributions {
    main {
        contents {
            from {
                ['README.md', 'RELEASE_NOTES.md', 'configuration/verify-service-provider.yml']
            }
        }
    }
}

task testAcceptance(type: Test) {
    testClassesDir = sourceSets.acceptanceTest.output.classesDir
    classpath += sourceSets.acceptanceTest.runtimeClasspath
}

run {
    args = ["server", "configuration/verify-service-provider.yml"]
}

mainClassName = 'uk.gov.ida.verifyserviceprovider.VerifyServiceProviderApplication'
